apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar, dependsOn: classes) { 
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) { 
  archiveClassifier.set('javadoc')
  from javadoc.destinationDir
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar

      pom {
        name = project.name
        description = project.description
        url = gitHubUrl
        licenses {
          license {
            name = 'LGPL 2.1 license'
            url = 'http://www.opensource.org/licenses/lgpl-2.1.php'
          }
        }
        developers {
          developer {
            id = 'kruton'
            name = 'Kenny Root'
            email = 'kenny@the-b.org'
          }
        }
        scm {
          connection = "${gitHubUrl}.git"
          developerConnection = "${gitHubUrl}.git"
          url = "https://github.com/ssrlive/jsocks"
        }
      }
    }
  }

  repositories {
    maven {
      url = uri("file://${buildDir}/repo")
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

// Load data from environment if present to support Travis
['bintrayUser', 'bintrayApiKey'].each {
  if (System.getenv(it)) {
    project.ext[it] = System.getenv(it)
  }
}

// This is only needed if bintrayUpload is done as part of releasing
task updateBintrayVersion {
  doLast {
    bintray.pkg.version.name = project.version
  }
}
bintrayUpload.dependsOn updateBintrayVersion

bintray {
  user = project.ext.bintrayUser
  key = project.ext.bintrayApiKey
  publish = true
  pkg {
    repo = 'maven'
    name = project.name

    licenses = ['LGPL']
    configurations = ['archives']

    websiteUrl = gitHubUrl
    vcsUrl = "${gitHubUrl}.git"
  }
}

bintrayUpload.onlyIf {
  System.getenv('TRAVIS_JDK_VERSION') == officialJdk &&
  !(version ==~ /.*SNAPSHOT/)
}